
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРеализацию(Команда)
    
    Объект.СписокДоговоров.Очистить();
    
    ДлительнаяОперация = ЗаполнитьНаСервере();
    
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
    ПараметрыОжидания.Интервал = 1;
    ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
    
    ОписаниеОЗавершении = Новый ОписаниеОповещения("ВыполнитьЗавершение", ЭтотОбъект);
    ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьНаСервере()

    ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьДокументыРеализаций", Объект.ПериодДляУказанияМесяца);
    
    Возврат ДлительнаяОперация;
    
КонецФункции

&НаКлиенте
Процедура ВыполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт

    Если Результат = Неопределено Тогда
        
        Возврат;
    
    КонецЕсли;
    
    Если Результат.Статус = "Выполнено" Тогда
        
        Если ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
                   
            МассивРеализаций = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
        
            ЗаполнитьДоговоры(МассивРеализаций);
            
        Иначе
            
            ТекстСообщения = НСтр("ru = 'ОБРАБОТКА: Массовое создание актов не выполнено.
                                        |Не получен адрес временного хранилища.'");
        
            ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ТекстСообщения));
            
        КонецЕсли;
    
    КонецЕсли;
       
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДоговоры(МассивРеализаций)

    Объект.СписокДоговоров.Очистить();

    Для Каждого Документ Из МассивРеализаций Цикл
        
        НоваяСтрока = Объект.СписокДоговоров.Добавить();
        НоваяСтрока.Договор = Документ.Договор;
        НоваяСтрока.РеализацияТоваровУслуг = Документ.Реализация;
    
    КонецЦикла;

КонецПроцедуры

#КонецОбласти
