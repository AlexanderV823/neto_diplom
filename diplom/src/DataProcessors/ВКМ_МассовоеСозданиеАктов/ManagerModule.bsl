
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоздатьДокументыРеализаций(Период) Экспорт
    
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |    ДоговорыКонтрагентов.Владелец КАК Контрагент,
        |    ДоговорыКонтрагентов.Ссылка КАК Ссылка,
        |    ДоговорыКонтрагентов.Организация КАК Организация
        |ПОМЕСТИТЬ ВТ_ДанныеДоговора
        |ИЗ
        |    Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
        |ГДЕ
        |    ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание)
        |    И &Период
        |        МЕЖДУ ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора И ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    РеализацияТоваровУслуг.Ссылка КАК Ссылка,
        |    РеализацияТоваровУслуг.Контрагент КАК Контрагент,
        |    РеализацияТоваровУслуг.Договор КАК Договор
        |ПОМЕСТИТЬ ВТ_ДанныеРеализации
        |ИЗ
        |    Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
        |ГДЕ
        |    РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
        |    И РеализацияТоваровУслуг.Дата МЕЖДУ &ПериодНачало И &ПериодКонец
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    ВТ_ДанныеДоговора.Ссылка КАК Договор,
        |    ВТ_ДанныеРеализации.Ссылка КАК Реализация,
        |    ВТ_ДанныеДоговора.Организация КАК Организация,
        |    ВТ_ДанныеДоговора.Контрагент КАК Контрагент
        |ИЗ
        |    ВТ_ДанныеДоговора КАК ВТ_ДанныеДоговора
        |        ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеРеализации КАК ВТ_ДанныеРеализации
        |        ПО ВТ_ДанныеДоговора.Ссылка = ВТ_ДанныеРеализации.Договор";
    
    Запрос.УстановитьПараметр("Период", Период);
    Запрос.УстановитьПараметр("ПериодНачало", НачалоМесяца(Период));
    Запрос.УстановитьПараметр("ПериодКонец", КонецМесяца(Период));
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    
    Сч = 0;
    
    МассивРеализаций = Новый Массив;
            
    Пока Выборка.Следующий() Цикл
        
        Сч = Сч + 1;
        
        ДлительныеОперации.СообщитьПрогресс(Цел(Сч / Выборка.Количество() * 100), "Выполняется длительная операция...");
        
        РеализацииСтруктура = Новый Структура;
        
        Если Не ЗначениеЗаполнено(Выборка.Реализация) Тогда
                
            НоваяРеализация = СозданиеРеализации(Выборка, КонецМесяца(Период));
            
            РеализацииСтруктура.Вставить("Договор", Выборка.Договор);
            РеализацииСтруктура.Вставить("Реализация", НоваяРеализация);
        
        Иначе                
                
            РеализацииСтруктура.Вставить("Договор", Выборка.Договор);
            РеализацииСтруктура.Вставить("Реализация", Выборка.Реализация);
            
        КонецЕсли;

        МассивРеализаций.Добавить(РеализацииСтруктура);
        
    КонецЦикла;
    
    Возврат МассивРеализаций;
    
КонецФункции

Функция СозданиеРеализации(Выборка, Дата)

    НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
    НовыйДокумент.Дата = Дата;
    НовыйДокумент.Договор = Выборка.Договор;
    НовыйДокумент.Контрагент = Выборка.Контрагент;
    НовыйДокумент.Организация = Выборка.Организация;
    НовыйДокумент.ВКМ_ВыполнитьАвтозаполнение(Выборка.Реализация);
    НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
    
    Если НовыйДокумент.ПроверитьЗаполнение() Тогда

        Попытка  
            
            НачатьТранзакцию();
            
            НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
            
            ЗафиксироватьТранзакцию();
        
        Исключение
            
            ОтменитьТранзакцию();
            
            ТекстСообщения = НСтр("ru = 'ОБРАБОТКА: Массовое создание актов отменено.
                                        |Не удалось провести документ: %1'");
        
            ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, Выборка.Реализация));
            
            ЗаписьЖурналаРегистрации(СтрШаблон(ТекстСообщения, Выборка.Реализация));
            
        КонецПопытки;

    Иначе
               
        ТекстСообщения = НСтр("ru = 'Не удалось создать документ реализации по Договору: %1.
                                    |Не все обязательные данные заполнены!'");
        
        ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, Выборка.Договор));
            
    КонецЕсли;

    Возврат НовыйДокумент.Ссылка;

КонецФункции

#КонецОбласти

#КонецЕсли
