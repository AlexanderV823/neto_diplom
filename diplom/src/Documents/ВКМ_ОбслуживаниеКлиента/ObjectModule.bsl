
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

    Перем ДатаРабот; //Хранит дату работ из ранее записанной версии документа
    Перем ВремяНачала; //Хранит время начала работ из ранее записанной версии документа 
    Перем ВремяОкончания; //Хранит время окончания работ из ранее записанной версии документа
    Перем НазначенныйСпециалист; //Хранит назначенного специалиста из ранее записанной версии документа
    Перем НовыйДокумент; //Истина -новый документ, ложь - повторно записываемый

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
    
    Если ОбменДанными.Загрузка Тогда
        
        Возврат;
    
    КонецЕсли;
    
    Если Не ЭтоНовый() Тогда
        
        Запрос = Новый Запрос;
        Запрос.Текст =
            "ВЫБРАТЬ
            |    ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
            |    ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
            |    ВКМ_ОбслуживаниеКлиента.ВремяНачалаРабот КАК ВремяНачалаРабот,
            |    ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРабот КАК ВремяОкончанияРабот
            |ИЗ
            |    Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
            |ГДЕ
            |    ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
        
        Запрос.УстановитьПараметр("Ссылка", Ссылка);
        
        РезультатЗапроса = Запрос.Выполнить();
        
        Выборка = РезультатЗапроса.Выбрать();
        
        Если Выборка.Следующий() Тогда
            
            ДатаРабот = Выборка.ДатаПроведенияРабот;
            ВремяНачала = Выборка.ВремяНачалаРабот;
            ВремяОкончания = Выборка.ВремяОкончанияРабот;
            НазначенныйСпециалист = Выборка.Специалист;
            НовыйДокумент = Ложь;
            
        КонецЕсли;
        
    Иначе
        
        НовыйДокумент = Истина;
           
    КонецЕсли;
        
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
    
    ПроцентОтРабот = ВКМ_ВыплатыСотрудникам.КоэффициентСотрудника(Специалист, Дата);
    
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |    ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
        |    ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_НачалоДействияДоговора КАК НачалоДействияДоговора,
        |    ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ОкончаниеДействияДоговора КАК ОкончаниеДействияДоговора,
        |    ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора КАК ВидДоговора
        |ИЗ
        |    Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
        |ГДЕ
        |    ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
    
    Запрос.УстановитьПараметр("Ссылка", Ссылка);
        
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    
    Пока Выборка.Следующий() Цикл
        
        Если Выборка.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание Тогда
            
            ТекстСообщения = НСтр("ru = 'Проверьте правильность выбора договора:
                                        |Должен быть выбран договор с типом Абонентское обслуживание.'");
       
            ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Договор", "Объект", Отказ);
            
            Возврат;
        
        КонецЕсли;
        
        Если Выборка.НачалоДействияДоговора > Дата Или Выборка.ОкончаниеДействияДоговора < Дата Тогда
            
            ТекстСообщения = НСтр("ru = 'Проверьте правильность выбора договора:
                                        |Дата документа должна быть в пределах срока действия договора.'");
       
            ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Договор", "Объект", Отказ);
            
            Возврат;
        
        КонецЕсли;
        
        Если ПроцентОтРабот = Неопределено Тогда
           
            ТекстСообщения = НСтр("ru = 'Специалисту не установлен процент оплаты от работ!'");
        
            ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Специалист", "Объект", Отказ);
            
            Возврат;
            
        КонецЕсли;
        
        // регистр ВКМ_ВыполненныеКлиентуРаботы
        Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
        
        // регистр ВКМ_ВыполненныеСотрудникомРаботы
        Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
        
        Для Каждого ТекСтрокаВыполненныеРаботы из ВыполненныеРаботы Цикл 
            
            // регистр ВКМ_ВыполненныеКлиентуРаботы
            Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
            Движение.Период = Дата;
            Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
            Движение.Клиент = Клиент;
            Движение.Договор = Договор;
            Движение.КоличествоЧасов = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
            Движение.СуммаКОплате = Выборка.СтоимостьЧасаРаботы * ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
            
            // регистр ВКМ_ВыполненныеСотрудникомРаботы
            Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
            Движение.Период = Дата;
            Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
            Движение.Сотрудник = Специалист;
            Движение.ЧасовКОплате = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
            Движение.СуммаКОплате = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту * Выборка.СтоимостьЧасаРаботы * ПроцентОтРабот / 100;
        
        КонецЦикла;
       
    КонецЦикла;
   
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
    
    Если ОбменДанными.Загрузка Тогда
        
        Возврат;
    
    КонецЕсли;    
    
    Если НовыйДокумент Тогда
        
        ТекстСообщения = НСтр("ru = 'Создан новый документ %1:
                                    |Дата работ: %2
                                    |Время начала работ: %3
                                    |Время окончания работ %4
                                    |Исполнитель: %5'");
       
        ТекстСообщенияВТелеграм = СтрШаблон(ТекстСообщения,
                                            Ссылка, 
                                            Формат(ДатаПроведенияРабот, "ДЛФ=DD;"), 
                                            Формат(ВремяНачалаРабот, "ДЛФ=DT;"), 
                                            Формат(ВремяОкончанияРабот, "ДЛФ=DDT;"),
                                            Специалист);
        
        НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
        НовыйЭлемент.ТекстСообщения = ТекстСообщенияВТелеграм;
        НовыйЭлемент.Записать();
        
    Иначе  
   
        Если ДатаРабот <> ДатаПроведенияРабот Тогда
                
            ТекстСообщения = НСтр("ru = 'В документе: %1 изменена дата проведения работ.
                                        |Новая дата: %2'");
               
            ТекстСообщенияВТелеграм = СтрШаблон(ТекстСообщения, Ссылка, ДатаПроведенияРабот);
                
            НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
            НовыйЭлемент.ТекстСообщения = ТекстСообщенияВТелеграм;
            НовыйЭлемент.Записать(); 
                    
        КонецЕсли;
            
        Если ВремяНачала <> ВремяНачалаРабот Или ВремяОкончания <> ВремяОкончанияРабот Тогда
                
            ТекстСообщения = НСтр("ru = 'В документе: %1 изменено время проведения работ.
                                        |Новое время с %2 по %3'");
               
            ТекстСообщенияВТелеграм = СтрШаблон(ТекстСообщения,
                                                Ссылка,
                                                Формат(ВремяНачалаРабот, "ДЛФ=DT;"),
                                                Формат(ВремяОкончанияРабот, "ДЛФ=DDT;"));
                
            НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
            НовыйЭлемент.ТекстСообщения = ТекстСообщенияВТелеграм;
            НовыйЭлемент.Записать(); 
                    
        КонецЕсли;
            
        Если НазначенныйСпециалист <> Специалист Тогда
            
            ТекстСообщения = НСтр("ru = 'В документе: %1 изменен специалист.
                                        |Новый ответственный: %2'");
           
            ТекстСообщенияВТелеграм = СтрШаблон(ТекстСообщения,
                                                Ссылка,
                                                Специалист);
            
            НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
            НовыйЭлемент.ТекстСообщения = ТекстСообщенияВТелеграм;
            НовыйЭлемент.Записать(); 
                
        КонецЕсли;
    
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли