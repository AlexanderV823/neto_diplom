
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции
    
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
// Начало Володин А.А. 
// Дипломная работа
// 19 февраля 2025 г.

    Команда = КомандыЗаполнения.Добавить();
    Команда.Идентификатор = "ЗаполнитьРаботы";
    Команда.Представление = "Заполнить";
    Команда.Порядок = 10;
    Команда.МножественныйВыбор = Ложь;
    Команда.РежимЗаписи = "НеЗаписывать";
    Команда.ВидимостьВФормах = "ФормаДокумента";
    Команда.Обработчик = "ВКМ_ВызватьАвтозаполнение";
    Команда.ИмяФормы = "Форма.ФормаДокумента";
     
// Конец 19 февраля 2025 г.

КонецПроцедуры

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г.        
    НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
// Конец 21 февраля 2025 г.
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г.
    
    // Акт
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.Идентификатор = "Акт";
    КомандаПечати.Представление = НСтр("ru = 'Акт оказанных услуг'");
    КомандаПечати.Порядок = 10;
    
// Конец 21 февраля 2025 г.   
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
  
    ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Акт");
    Если ПечатнаяФорма <> Неопределено Тогда
        ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
        ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказании услуг'");
        ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ЗаказПокупателя.ПФ_MXL_Акт";
    КонецЕсли;
    
// Конец 21 февраля 2025 г.     
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
   
    ТабличныйДокумент = Новый ТабличныйДокумент;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
    
    Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
    
    ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
    
    ПервыйДокумент = Истина;
    
    Пока ДанныеДокументов.Следующий() Цикл
        
        Если Не ПервыйДокумент Тогда
            // Все документы нужно выводить на разных страницах.
            ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
        КонецЕсли;
        
        ПервыйДокумент = Ложь;
        
        НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
        
        ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
        
        ВывестиШапкуАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
        
        ВывестиТоварыУслугиАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
        
        ВывестиСуммуПрописьюАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
        
        ВывестиПодписиСторонАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
        
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);     
        
    КонецЦикла; 
        
    Возврат ТабличныйДокумент;
   
// Конец 21 февраля 2025 г.   
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
    
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
    |    РеализацияТоваровУслуг.Ссылка КАК Ссылка,
    |    РеализацияТоваровУслуг.Номер КАК Номер,
    |    РеализацияТоваровУслуг.Дата КАК Дата,
    |    РеализацияТоваровУслуг.Организация КАК Организация,
    |    РеализацияТоваровУслуг.Контрагент КАК Контрагент,
    |    РеализацияТоваровУслуг.Договор КАК Договор,
    |    РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
    |    РеализацияТоваровУслуг.Ответственный КАК Ответственный,
    |    РеализацияТоваровУслуг.Товары.(
    |        Ссылка КАК Ссылка,
    |        НомерСтроки КАК НомерСтроки,
    |        Номенклатура КАК Номенклатура,
    |        Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
    |        Количество КАК Количество,
    |        Цена КАК Цена,
    |        Сумма КАК Сумма) КАК Товары,
    |    РеализацияТоваровУслуг.Услуги.(
    |        Ссылка КАК Ссылка,
    |        НомерСтроки КАК НомерСтроки,
    |        Номенклатура КАК Номенклатура,
    |        Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
    |        Количество КАК Количество,
    |        Цена КАК Цена,
    |        Сумма КАК Сумма) КАК Услуги
    |ИЗ
    |    Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
    |ГДЕ
    |    РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
    
    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
    
    Возврат Запрос.Выполнить().Выбрать();

// Конец 21 февраля 2025 г.     
КонецФункции

Процедура ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
    
    ОбластьЗаголовокАкта = Макет.ПолучитьОбласть("Заголовок");
    
    ДанныеПечати = Новый Структура;
    
    ШаблонЗаголовка = "Акт № %1 от %2";
    ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
        ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
        Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
    ДанныеПечати.Вставить("Акт", ТекстЗаголовка);
    
    ОбластьЗаголовокАкта.Параметры.Заполнить(ДанныеПечати);
    ТабличныйДокумент.Вывести(ОбластьЗаголовокАкта);
    
// Конец 21 февраля 2025 г.     
КонецПроцедуры

Процедура ВывестиШапкуАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
   
    ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
    
    ДанныеПечати = Новый Структура;
    ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
    ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
    ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
    
    ОбластьШапка.Параметры.Заполнить(ДанныеПечати);
    ТабличныйДокумент.Вывести(ОбластьШапка);

// Конец 21 февраля 2025 г.     
КонецПроцедуры

Процедура ВывестиТоварыУслугиАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
   
    ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТЧ");
    ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТЧ");
    ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
    
    ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
    
    ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
    Пока ВыборкаТовары.Следующий() Цикл
        ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
        ТабличныйДокумент.Вывести(ОбластьСтрока);
    КонецЦикла;
    
    ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
    Пока ВыборкаУслуги.Следующий() Цикл
        ОбластьСтрока.Параметры.Заполнить(ВыборкаУслуги);
        ТабличныйДокумент.Вывести(ОбластьСтрока);
    КонецЦикла;
   
    ДанныеПечати = Новый Структура;
    ДанныеПечати.Вставить("Итого", ДанныеДокументов.СуммаДокумента);
    
    ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);
    
    ТабличныйДокумент.Вывести(ОбластьПодвал);

// Конец 21 февраля 2025 г.     
КонецПроцедуры

Процедура ВывестиСуммуПрописьюАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
    
    ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
    
    СуммаДокументаПрописью = ФорматироватьСуммуПрописи(ДанныеДокументов.СуммаДокумента,Цел(ДанныеДокументов.СуммаДокумента));
    
    ДанныеПечати = Новый Структура;
    ДанныеПечати.Вставить("СуммаПрописью", СуммаДокументаПрописью);
    
    ОбластьСуммаПрописью.Параметры.Заполнить(ДанныеПечати);

    ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);

// Конец 21 февраля 2025 г.     
КонецПроцедуры

Процедура ВывестиПодписиСторонАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
    
    ОбластьПодписиСторон = Макет.ПолучитьОбласть("ПодписиСторон");
    
    ТабличныйДокумент.Вывести(ОбластьПодписиСторон);

// Конец 21 февраля 2025 г.     
КонецПроцедуры

Функция ФорматироватьСуммуПрописи(СуммаДок, СуммаБезКопеек)
// Начало Володин А.А. 
// Дипломная работа
// 21 февраля 2025 г. 
    
    Результат     = СуммаДок;
    ЦелаяЧасть    = Цел(СуммаДок);
    ФорматСтрока  = "Л=ru_RU; ДП=Ложь";
    ПарамПредмета = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж";
    
    Если (Результат - ЦелаяЧасть) = 0 Тогда
        Если СуммаБезКопеек Тогда
            Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
            Результат = Лев(Результат,Найти(Результат,"0")-1)+"00 копеек";
        Иначе
            Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
        КонецЕсли;
    Иначе
        Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
    КонецЕсли;
    
    Возврат Результат;
    
// Конец 21 февраля 2025 г.    
КонецФункции

#КонецОбласти

#КонецЕсли